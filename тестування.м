структура ФактичнийОбєкт
  значення
  навпаки
  провалити_припущення
кінець

дія Припустити(фактичний_обєкт)
  дія провалити(факт, очікуваний_обєкт, повідомлення)
    якщо факт.навпаки
      впасти "%(повідомлення).\nОчікувано: усе, крім %(очікуваний_обєкт)\nФактично: %(фактичний_обєкт)"
    інакше
      впасти "%(повідомлення).\nОчікувано: %(очікуваний_обєкт)\nФактично: %(фактичний_обєкт)"
    кінець
  кінець

  вернути ФактичнийОбєкт(значення=фактичний_обєкт, навпаки=ні, провалити_припущення=провалити)
кінець

дія ФактичнийОбєкт.не_()
  я.навпаки = !я.навпаки

  вернути я
кінець

дія ФактичнийОбєкт.дорівнює(очікуваний_обєкт)
  якщо (!я.навпаки і (я.значення != очікуваний_обєкт))
    я.провалити_припущення(я, очікуваний_обєкт, "Очікується, що об'єкти рівні")
  якщо (я.навпаки і (я.значення == очікуваний_обєкт))
    я.провалити_припущення(я, очікуваний_обєкт, "Очікується, що об'єкти не є рівні")
  кінець
кінець

дія ФактичнийОбєкт.є_пусто()
  якщо (!я.навпаки і (я.значення не є пусто))
    я.провалити_припущення(я, пусто, "Очікується, що об'єкт є пусто")
  якщо (я.навпаки і (я.значення є пусто))
    я.провалити_припущення(я, пусто, "Очікується, що об'єкт не є пусто")
  кінець
кінець

дія ФактичнийОбєкт.є_вірне()
  якщо (!я.навпаки і (я.значення != так))
    я.провалити_припущення(я, так, "Очікується, що твердження є вірне")
  якщо (я.навпаки і (я.значення == так))
    я.провалити_припущення(я, так, "Очікується, що твердження не є вірне")
  кінець
кінець

дія тестовий_випадок(назва текст, сценарій Дія)
  спробувати
    сценарій(Припустити)
  зловити п
    друк("%(назва) [провалено]:")
    друк("%(п)\n")
    вернути
  кінець

  друк("%(назва) [задовільно]\n")
кінець

дати тестовий_випадок
